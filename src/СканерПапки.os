#Использовать json
#Использовать ".."

Перем Настройки;
Перем ТаблицаКонтроля;
Перем Обработчики;
Перем Статусы;

Процедура ЗапуститьКонтроль()

	НачалоКонтроля = ТекущаяДата();
	Сообщить("Начало контроля: " + НачалоКонтроля);

	Если ЗначениеЗаполнено(Настройки.ДлительностьОпроса) Тогда 
		
		ОкончаниеКонтроля = НачалоКонтроля + Настройки.ДлительностьОпроса;
		Сообщить("Ожидаемое окончание контроля: " + ОкончаниеКонтроля);
		УсловиеЦиклаКонтроля = "ТекущаяДата() < ОкончаниеКонтроля";
	
	Иначе

		Сообщить("Окончание контроля: <не задано>");
		УсловиеЦиклаКонтроля = "Истина";

	КонецЕсли;
	
	Пока Вычислить(УсловиеЦиклаКонтроля) Цикл
		ВыполнитьКонтроль();
		Приостановить(Настройки.ПериодОпроса * 1000);
		Настройки.ПропуститьПервыйКонтроль = Ложь;
	КонецЦикла;

	Сообщить("Окончание контроля: " + ТекущаяДата());

КонецПроцедуры

Процедура ВыполнитьКонтроль()

	Для каждого Стр Из ТаблицаКонтроля Цикл
		Стр.Статус = Статусы.Удален;
	КонецЦикла;

	мФайлы = НайтиФайлы(Настройки.ОбъектКонтроля, ПолучитьМаскуВсеФайлы(), Настройки.Рекурсивно);
	Для каждого Файл Из мФайлы Цикл
		ИмяФайла = Файл.ПолноеИмя;
		
		Стр = ТаблицаКонтроля.Найти(ИмяФайла, "ИмяФайла");
		СвойстваФайла = СвойстваФайла(Файл);
		Если Стр = Неопределено Тогда
			Стр = ТаблицаКонтроля.Добавить();
			ЗаполнитьЗначенияСвойств(Стр, СвойстваФайла);
			Стр.Статус = Статусы.Новый;
		ИначеЕсли Стр.Размер <> СвойстваФайла.Размер
			Или Стр.ДатаИзменения <> СвойстваФайла.ДатаИзменения Тогда
			ЗаполнитьЗначенияСвойств(Стр, СвойстваФайла);
			Стр.Статус = Статусы.Изменен;
		Иначе
			Стр.Статус = Статусы.НеИзменился;
		КонецЕсли;

	КонецЦикла;

	мСнятьСКонтроля = Новый Массив;
	мКОбработке = Новый Массив;
	Для каждого Стр Из ТаблицаКонтроля Цикл
		Если Стр.Статус = Статусы.Удален Тогда
			мСнятьСКонтроля.Добавить(Стр);
		ИначеЕсли Стр.Статус = Статусы.Изменен
			Или Стр.Статус = Статусы.Новый Тогда
			Если НЕ Настройки.ПропуститьПервыйКонтроль Тогда
				мКОбработке.Добавить(Стр);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Для каждого Стр Из мСнятьСКонтроля Цикл
		ТаблицаКонтроля.Удалить(Стр);
	КонецЦикла;

	ВыполнитьОбработкуФайла(мКОбработке);

КонецПроцедуры

Функция СвойстваФайла(Файл)
	сткСвойства = Новый Структура();
	сткСвойства.Вставить("ИмяФайла", Файл.ПолноеИмя);
	Если НЕ Файл.ЭтоКаталог() Тогда
		сткСвойства.Вставить("Размер", Файл.Размер());
		сткСвойства.Вставить("ЭтоКаталог", Ложь);
	Иначе
		сткСвойства.Вставить("Размер", 0);
		сткСвойства.Вставить("ЭтоКаталог", Истина);
	КонецЕсли;
	сткСвойства.Вставить("ДатаИзменения", Файл.ПолучитьВремяИзменения());
	Возврат сткСвойства;
КонецФункции

Процедура ВыполнитьОбработкуФайла(мКОбработке)

	Если НЕ ЗначениеЗаполнено(мКОбработке) Тогда
		Возврат;
	КонецЕсли;

	Если Настройки.ОбрабатыватьПоОдному Тогда
		Для каждого СтрокаТаблицыКонтроля Из мКОбработке Цикл
			Сообщить(СтрокаТаблицыКонтроля.ИмяФайла + " - " + СтрокаТаблицыКонтроля.Статус);
			Архиватор = Новый Архиватор(СтрокаТаблицыКонтроля.ИмяФайла);
			Архиватор.ВыполнитьДействие(Настройки.Назначение);
		КонецЦикла;
	Иначе
		мИменаФайлов = Новый Массив();
		Для каждого СтрокаТаблицыКонтроля Из мКОбработке Цикл
			Сообщить(СтрокаТаблицыКонтроля.ИмяФайла + " - " + СтрокаТаблицыКонтроля.Статус);
			мИменаФайлов.Добавить(СтрокаТаблицыКонтроля.ИмяФайла);
		КонецЦикла;
		Архиватор = Новый Архиватор(мИменаФайлов);
		Архиватор.ВыполнитьДействиеСМассивомФайлов(Настройки.Назначение);
	КонецЕсли;
	Архиватор = Неопределено;
КонецПроцедуры

Процедура Инициализировать()

	ТаблицаКонтроля = Новый ТаблицаЗначений();
	
	ТаблицаКонтроля.Колонки.Добавить("ИмяФайла");
	ТаблицаКонтроля.Колонки.Добавить("ЭтоКаталог");
	ТаблицаКонтроля.Колонки.Добавить("Размер");
	ТаблицаКонтроля.Колонки.Добавить("ДатаИзменения");
	ТаблицаКонтроля.Колонки.Добавить("Статус");

	сткСтатусы = Новый Структура();
	сткСтатусы.Вставить("Новый", 1);
	сткСтатусы.Вставить("Изменен", 2);
	сткСтатусы.Вставить("Удален", 3);
	сткСтатусы.Вставить("НеИзменился", 0);
	Статусы = Новый ФиксированнаяСтруктура(сткСтатусы);

КонецПроцедуры

Настройки = Новый Структура();
Настройки.Вставить("ОбъектКонтроля", ".");
Настройки.Вставить("Назначение", "..");
Настройки.Вставить("Рекурсивно", Истина);
Настройки.Вставить("ПериодОпроса", 5);
Настройки.Вставить("ДлительностьОпроса", 60);
Настройки.Вставить("ПропуститьПервыйКонтроль", Истина);
Настройки.Вставить("ОбрабатыватьПоОдному", Ложь);

Если АргументыКоманднойСтроки.Количество() > 0 Тогда
	Файл = Новый Файл(АргументыКоманднойСтроки[0]);
	Если Файл.Существует() Тогда
		Если Файл.ЭтоКаталог() Тогда
			Настройки.ОбъектКонтроля = АргументыКоманднойСтроки[0];
			Если АргументыКоманднойСтроки.Количество() > 1 Тогда
				Настройки.Назначение = АргументыКоманднойСтроки[1];
			КонецЕсли;
		ИначеЕсли Нрег(Файл.Расширение) = ".json" Тогда
			ЧтениеТекста = Новый ЧтениеТекста(Файл.ПолноеИмя, КодировкаТекста.UTF8);
			Текст = ЧтениеТекста.Прочитать();
			ЧтениеТекста.Закрыть();
			ПарсерJSON = Новый ПарсерJSON();
			НастройкиJson = ПарсерJSON.ПрочитатьJSON(Текст,,,Истина);
			ЗаполнитьЗначенияСвойств(Настройки, НастройкиJson);
		КонецЕсли;
	КонецЕсли;
КонецЕсли;

Инициализировать();

ЗапуститьКонтроль();
