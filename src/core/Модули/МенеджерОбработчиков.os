#Использовать logos

Перем Лог;
Перем мОбработчики;
Перем ТолькоМониторинг;

Процедура ОписаниеКоманды(Команда) Экспорт
	
	Для каждого Обработчик Из мОбработчики Цикл
		Обработчик.ОписаниеКоманды(Команда);
	КонецЦикла;

КонецПроцедуры

Процедура ПриЧтенииПараметров(Команда) Экспорт
	
	Для каждого Обработчик Из мОбработчики Цикл
		Обработчик.ПриЧтенииПараметров(Команда);
	КонецЦикла;
	
	ПослеЧтенияПараметров();

КонецПроцедуры

Процедура ПослеЧтенияПараметров()
	ТолькоМониторинг = Истина;
	Для каждого Обработчик Из мОбработчики Цикл
		Обработчик.ПослеЧтенияПараметров();
		Если Обработчик.Включен() Тогда
			ТолькоМониторинг = Ложь;
		КонецЕсли;
	КонецЦикла;
	Если ТолькоМониторинг Тогда
		Лог.Предупреждение("ТОЛЬКО МОНИТОРИНГ. Обработчики не подключены!");
	КонецЕсли;
КонецПроцедуры

Процедура ВыполнитьОбработчики(МассивФайлов) Экспорт
	Если ТолькоМониторинг Тогда
		Для каждого СтрокаТаблицыКонтроля Из МассивФайлов Цикл
			Лог.Информация("%1: %2", СтрокаТаблицыКонтроля.Статус, СтрокаТаблицыКонтроля.Имя);
		КонецЦикла;
	Иначе
		Для каждого Обработчик Из мОбработчики Цикл
			Если Обработчик.Включен() Тогда
				Попытка
					Обработчик.ВыполнитьДействие(МассивФайлов);
				Исключение
					Лог.Ошибка("Выполнение обработчика %1 вызвало исключение. Обработчик пропущен.
							|  - %2", Обработчик.Имя(), КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
				КонецПопытки;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

Процедура ПодключитьОбработчик(Объект)
	мОбработчики.Добавить(Объект);
КонецПроцедуры

Лог = Логирование.ПолучитьЛог(ПараметрыПриложения.ИмяЛога());

мОбработчики = Новый Массив;
ПодключитьОбработчик(Новый Архиватор());
ПодключитьОбработчик(Новый Версионирование());
ПодключитьОбработчик(Новый Внешний());
