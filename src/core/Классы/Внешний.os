#Использовать fs
#Использовать logos

Перем Лог;
Перем Параметры;
Перем ПутьКОбработчику;
Перем Включен;
Перем ВнешнийКласс;
Перем ИмяВнешнегоСценария;
Перем Рефлектор;

Функция Имя() Экспорт
	Если ЗначениеЗаполнено(ИмяВнешнегоСценария) Тогда
		Возврат ИмяВнешнегоСценария;
	Иначе
		Возврат "Внешний сценарий";
	КонецЕсли;
КонецФункции

Процедура ОписаниеКоманды(Команда) Экспорт

	Команда.Опция("ext external", "", Имя() + ": путь к внешнему скрипту-обработчику")
				.ТСтрока();

	Команда.Опция("ent entrypoint", "ВыполнитьДействие", Имя() + ": переопределение основного метода вызова (точки входа) внешнего скрипта-обработчика")
				.ТСтрока();

КонецПроцедуры

Процедура ПриЧтенииПараметров(Команда) Экспорт

	Параметры = Новый Структура();
	Параметры.Вставить("Путь", Команда.ЗначениеОпции("external"));
	Параметры.Вставить("ТочкаВхода", Команда.ЗначениеОпции("entrypoint"));

	Включен = ЗначениеЗаполнено(Параметры.Путь);

КонецПроцедуры

Процедура ПослеЧтенияПараметров() Экспорт

	Если Включен() Тогда
		
		Файл = Новый Файл(Параметры.Путь);
		Если НЕ Файл.Существует() Тогда
			Лог.КритичнаяОшибка("Внешний обработчик НЕ СУЩЕСТВУЕТ %1", Файл.ПолноеИмя);
			Включен = Ложь;
		ИначеЕсли Файл.Расширение <> ".os" Тогда
			Лог.КритичнаяОшибка("Внешний обработчик не является сценарием oscript: %1", Файл.ПолноеИмя);
			Включен = Ложь;
		Иначе
			ИмяВнешнегоСценария = Файл.Имя;
			СоздатьКлассВнешнегоОбработчика(Файл.ПолноеИмя);
		КонецЕсли;
		
		Если Включен Тогда
			Лог.Информация("Включен обработчик %1. Путь: %2", Имя(), Файл.ПолноеИмя);
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

Функция Включен() Экспорт
	Возврат Включен;
КонецФункции

Процедура СоздатьКлассВнешнегоОбработчика(ИмяФайла)

	ПодключитьСценарий(ИмяФайла, "ВнешнийКласс");
	Попытка
		ВнешнийКласс = Новый ("ВнешнийКласс");
		Лог.Отладка("Подключен внешний обработчик из файла %1", ИмяФайла);
	Исключение
		Лог.КритичнаяОшибка("Не удалось подключить внешний обработчик %1", ИмяФайла);
		Включен = Ложь;
	КонецПопытки;
	
	Если НЕ Включен Тогда
		Возврат;
	КонецЕсли;

	Если НЕ Рефлектор.МетодСуществует(ВнешнийКласс, Параметры.ТочкаВхода) Тогда
		Лог.КритичнаяОшибка("Не определена точка входа %1 внешнего обработчика %2", Параметры.ТочкаВхода, ИмяФайла);
		Включен = Ложь;
	ИначеЕсли Рефлектор.МетодСуществует(ВнешнийКласс, "Имя") Тогда
		ИмяВнешнегоСценария = Рефлектор.ВызватьМетод(ВнешнийКласс, "Имя");
		Лог.Отладка("Имя внешнего обработчик: %1", ИмяВнешнегоСценария);
	КонецЕсли;

КонецПроцедуры

Процедура ВыполнитьДействие(мФайлыКОбработке) Экспорт

	Для каждого СтрокаТаблицыКонтроля Из мФайлыКОбработке Цикл
		Лог.Информация("%1: %2 - передан для обработки в %3", СтрокаТаблицыКонтроля.Статус, СтрокаТаблицыКонтроля.Имя, ИмяВнешнегоСценария);
	КонецЦикла;

	МассивПараметров = Новый Массив();
	МассивПараметров.Добавить(мФайлыКОбработке);

	Рефлектор.ВызватьМетод(ВнешнийКласс, Параметры.ТочкаВхода, МассивПараметров);

КонецПроцедуры

Лог = Логирование.ПолучитьЛог(ПараметрыПриложения.ИмяЛога());
Рефлектор = Новый Рефлектор();