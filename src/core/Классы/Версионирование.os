#Использовать fs
#Использовать gitrunner


Перем Параметры;
Перем ГитРепозиторий;

Процедура ОписаниеКоманды(Команда) Экспорт

	Команда.Опция("gitrepopath", "", "копировать в гит-репозиторий")
				.ТСтрока();
	
	Команда.Опция("gitreposrc", "", "папка в гит-репозитории (относительно его корня)")
				.ТСтрока();

КонецПроцедуры

Процедура ПриЧтенииПараметров(Команда) Экспорт

	Параметры = Новый Структура();
	Параметры.Вставить("Путь", Команда.ЗначениеОпции("gitrepopath"));
	Параметры.Вставить("Папка", Команда.ЗначениеОпции("gitreposrc"));

КонецПроцедуры

Процедура ПослеЧтенияПараметров() Экспорт

	Если Включен() Тогда
		
		ФС.ОбеспечитьКаталог(Параметры.Путь);

		ГитРепозиторий = Новый ГитРепозиторий();
		ГитРепозиторий.УстановитьРабочийКаталог(Параметры.Путь);

		Если НЕ ГитРепозиторий.ЭтоРепозиторий() Тогда
			ГитРепозиторий.Инициализировать();
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

Функция Включен() Экспорт
	Возврат ЗначениеЗаполнено(Параметры.Путь);
КонецФункции

Процедура ВыполнитьДействие(мФайлыКОбработке) Экспорт

	Для каждого СтрокаТаблицыКонтроля Из мФайлыКОбработке Цикл
		
		Источник = СтрокаТаблицыКонтроля.ПолноеИмя;
		ПутьОтносительноКорняРепозитория = ОбъединитьПути(Параметры.Папка, СтрокаТаблицыКонтроля.Имя);
		Приемник = ОбъединитьПути(Параметры.Путь, ПутьОтносительноКорняРепозитория);
		
		КопироватьФайл(Источник, Приемник);

		ГитРепозиторий.ДобавитьФайлВИндекс(ПутьОтносительноКорняРепозитория);

	КонецЦикла;

	Статус = ГитРепозиторий.Статус(Истина);
	Если НЕ ПустаяСтрока(Статус) Тогда
		Комментарий = Статус;
		ГитРепозиторий.Закоммитить(Комментарий);
	КонецЕсли;

КонецПроцедуры
